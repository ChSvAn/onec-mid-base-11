
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++Черкашина С.А
	
	//Добавляем группу
	Группа = Элементы.Добавить("ГруппаСкидка", Тип("ГруппаФормы"), Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	Группа.ОтображатьЗаголовок = Ложь;
	Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	//Добавляем реквизит
	ПолеВввода = Элементы.Вставить("СогласованнаяСкидка", Тип("ПолеФормы"), Элементы.ГруппаСкидка);
	ПолеВввода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВввода.ПутьКДанным = "Объект.СогласованнаяСкидка";
	ПолеВввода.Заголовок = "Согласованная скидка";
	ПолеВввода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
	//Добавляем команду
	Команда = Команды.Добавить("ПересчитатьТаблицу");
	Команда.Заголовок = "Пересчитать таблицу";
	Команда.Действие = "КомандаПересчитатьТаблицу";
	
	//Добавляем кнопку 
	КнопкаФормы = Элементы.Добавить("КнопкаПересчитатьТаблицу", Тип("КнопкаФормы"), Элементы.ГруппаСкидка);
	КнопкаФормы.ИмяКоманды = "ПересчитатьТаблицу";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаФормы.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;	
	//--	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	//++Черкашина С.А.
	//Исходный код
	
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100;	
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
	//Новый код
	КоэффициентСкидки = 1 - (ТекущиеДанные.Скидка + Объект.СогласованнаяСкидка) / 100;
	
	СуммарнаяСкидка = ТекущиеДанные.Скидка + Объект.СогласованнаяСкидка;
	
	Если СуммарнаяСкидка <= 100 Тогда		
		ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;		
	Иначе
		ТекущиеДанные.Сумма = 0;			
	КонецЕсли;	
	//--	

	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область ДЗ_Доработка
&НаКлиенте
Процедура КомандаПересчитатьТаблицу(Команда)
	
	ПересчитатьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьТаблицы()
	
	Для Каждого ТекущиеДанные Из Объект.Товары Цикл
		РассчитатьСуммуСтроки(ТекущиеДанные);
	КонецЦикла;
	
	Для Каждого ТекущиеДанные Из Объект.Услуги Цикл	
		РассчитатьСуммуСтроки(ТекущиеДанные);		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СогласованнаяСкидкаПриИзменении(Элемент)
	
	Обращение = ВопросАсинх(НСтр("ru = 'Изменен процент скидки. Пересчитать таблицы товаров и услуг?'"), 
	РежимДиалогаВопрос.ДаНет, 0);
	Ответ = Ждать Обращение;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПересчитатьТаблицы();
		
	Иначе   
		Возврат;        
	КонецЕсли;	
	
КонецПроцедуры 

#КонецОбласти 

